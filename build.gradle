plugins {
    id "com.jfrog.bintray" version "1.7"
    id "com.gradle.plugin-publish" version "0.9.4"
}

apply plugin: 'groovy'
apply plugin: "com.gradle.plugin-publish"
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

dependencies {
    compile gradleApi()
    compile localGroovy()
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
}

group = 'io.splitest'
version = '0.1.5'

pluginBundle {
  website = 'https://github.com/rafoli/splitest'
  vcsUrl = 'http://github.com/rafoli/splitest'
  description = 'Splitest!'
  tags = ['splitest', 'unittest', 'integrationtest', 'funtionaltest']

  plugins {
    splitestPlugin {
      id = 'io.splitest'
      displayName = 'Splitest plugin'
    }
  }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar 
            artifact javadocJar
        }
    }
}

bintray {   
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['mavenJava']
	pkg {
		repo = 'maven'
		name = 'io.splitest'
		desc = 'Splitest'
		licenses = ['Apache-2.0']
		vcsUrl = 'http://github.com/rafoli/splitest.git'
		labels = ['splitest', 'unittest', 'integrationtest', 'funtionaltest']
		publicDownloadNumbers = true
	    version {
	        name = project.version
	        desc = 'Splitest'
	        released  = new Date()
	        vcsTag = project.version
	        attributes = ['gradle-plugin': 'gradle.plugin.io.splitest:splitest']
	    }
	}
}
